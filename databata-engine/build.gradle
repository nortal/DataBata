defaultTasks 'jar'
apply plugin: 'osgi'
apply from: project.rootDir.path + "/gradle-include/bintray.gradle"

description = "DataBata Engine"

dependencies {
	compile files("lib/sqltool.jar")
	compile 'commons-lang:commons-lang:2.4'
	compile 'log4j:log4j:[1.2,)'
	compile 'org.springframework:org.springframework.jdbc:3.1.3.RELEASE'
	compile 'org.springframework.osgi:spring-osgi-core:1.2.1'
	compile 'org.osgi:org.osgi.core:4.3.1'

	runtime 'com.oracle:ojdbc6:11.2.0.3'
	compile 'org.postgresql:com.springsource.org.postgresql.jdbc4:8.3.604'
	runtime 'org.hsqldb:hsqldb:2.2.9'
	runtime 'commons-logging:commons-logging:1.1.3'
}

// Add sourceJar and javadocJar to artifacts that will be published
artifacts {
	archives sourcesJar
	archives javadocJar
}

jar.doFirst {
	sourceSets.main.java.srcDirs.each { from it }
}

jar {
	include('**/*.java')
	include('**/*.class')
	include('**/*.xml')
	include('**/*.sql')
	include('**/*.properties')
	exclude('databata.properties')
	exclude('main/**')
	manifest { instruction 'Export-Package', 'META-INF.databata, eu.databata.engine, *' }
}

// Customize pom.xml to match the requirements of maven central
install {
	repositories.mavenInstaller { customizePom(pom, project) }
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->

		// add all items necessary for maven central publication
		generatedPom.project {
			name 'DataBata'
			description 'Incremental database change propagation tool'
			url 'https://github.com/nortal/DataBata'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			developers {
				developer {
					id 'baxboiko'
					name 'Maksim Boiko'
					email 'max.boiko@gmail.com'
				}
			}
			inceptionYear '2014'
			scm {
				url 'https://github.com/nortal/DataBata'
				connection 'scm:git:git@github.com:nortal/DataBata.git'
				developerConnection 'scm:git:git@github.com:nortal/DataBata.git'
			}
		}
	}
}